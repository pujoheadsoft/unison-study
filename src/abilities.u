
effectfulPredicate.stopIfTrue : (a -> {g} Boolean) -> a -> {g, Abort} a
effectfulPredicate.stopIfTrue pred a =
  if pred a then Abort.abort else a

store.stopIfTrue : (a -> Boolean) -> a -> {Abort, Store a} a
store.stopIfTrue pred a =
  Store.put a
  if pred a then Abort.abort else a

-- これはエラーになる(↓のように定義するとエラーにならない)
-- nonEmptyName : Text -> {} Text
-- nonEmptyName name =
--   stopIfTrue (text -> text === "") name

usingAblitiesPt1.nonEmptyName : Text -> Text
usingAblitiesPt1.nonEmptyName name =
  -- 関数定義
  optionalName : Optional Text
  optionalName =
    toOptional! do
      effectfulPredicate.stopIfTrue (text -> text === "") name
  Optional.getOrElse "Unknown Name" optionalName

store.nonEmptyName : Text -> Text
store.nonEmptyName name =
  storeIsHandled : '{Abort} Text
  storeIsHandled =
    do
      withInitialValue "Store Default Value" do
        store.stopIfTrue (text -> text === "") name
  abortIsHandled : Optional Text
  abortIsHandled = toOptional! storeIsHandled
  Optional.getOrElse "Optional Default Value" abortIsHandled

-- abilitiesはファイルのトップレベルで未処理のままにしておくことができない
-- 以下のように遅延させればよい
-- tryEmit : {Stream Text}()
-- tryEmit = Stream.emit "Hi"

tryEmit1 : '{Stream Text}()
tryEmit1 = do Stream.emit "Hello World"

tryEmit2 : '{Stream Text}()
tryEmit2 _ =
  Stream.emit "Hello World"

tryEmit3 : '{Stream Text}()
tryEmit3 = '(Stream.emit "Hello World")

nameGreet : '{IO, Exception} ()
nameGreet _ =
  use Text ++
  printLine "Enter your name:"
  name = readLine()
  printLine ("Hello " ++ name)


unique ability Ask a where
  ask : a

unique ability Emit a where
  emit : a -> ()

ask.handler : a -> Request {e, Ask a} r -> {e} r
ask.handler v = cases
  { a }            -> a
  { Ask.ask -> k } -> handle k v with ask.handler v

emit.handler : Request {e, Emit Text} r -> {e, IO, Exception} r
emit.handler = cases
  { a } -> a
  { Emit.emit msg -> k } -> handle
    printLine("Emit " ++ msg)
    k ()
  with emit.handler

ask.example : '{Ask Text, IO, Exception} ()
ask.example = do
  v = Ask.ask
  printLine v

emit.example : '{Emit Text, IO, Exception} ()
emit.example = do Emit.emit "Hello"

ask.runExample : '{IO, Exception} ()
ask.runExample = do
  handle ask.example() with ask.handler "Hello"

emit.runExample : '{IO, Exception} ()
emit.runExample = do
  handle emit.example() with emit.handler

composed.emitAsk : '{Emit a, Ask a, IO, Exception} ()
composed.emitAsk = do
  printLine("Start")
  askValue = Ask.ask
  Emit.emit askValue
  printLine("End")

composed.runEmitAsk : '{IO, Exception} ()
composed.runEmitAsk = do
  handle
    handle composed.emitAsk()
    with ask.handler "Ask Value"
  with emit.handler

